handle SIGHUP nostop print pass
handle SIGPIPE nostop print nopass

set print array on
set print pretty on
set print demangle on
set print object on
set print vtbl on

set $_exitcode = -999

printf "===\n=== Running...\n===\n"
run

if $_exitcode != -999
    printf "===\n=== Exiting...\n===\n"
    quit
else
    printf "===\n=== Trying to obtain a backtrace...\n===\n"
    backtrace
    printf "===\n=== Trying to obtain a list of threads...\n===\n"
    info threads
    printf "===\n=== Trying to obtain more verbose frame information...\n===\n"
    while (1)
        printf "*\n*    FRAME INFORMATION\n*\n*\n"
        info frame
        printf "*\n*    LOCAL VARIABLES\n*\n*\n"
        info locals
        up-silently
    end
end


